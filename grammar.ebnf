program           -> declaration* EOF ;
declaration       -> funDecl
                  |  varDecl
                  |  statement ;
funDecl           -> "fun" function ;
function          -> IDENTIFIER? "(" parameters? ")" block ( funcCall )? ;
parameters        -> IDENTIFIER ("," IDENTIFIER)* ;
varDecl           -> "var" IDENTIFIER ( "=" expression )? ;
statement         -> exprStatementNode
                  | ifStatement
                  | whileStatement
                  | breakStatement
                  | continueStatement
                  | returnStatement
                  | deferStatement
                  | assertStatement
                  | importStatement
                  | block ;
exprStatementNode -> expression
ifStatement       -> "if" "(" expression ")" statement ( "else" statement )? ;
whileStatement    -> "while" "(" expression ")" statement ;
breakStatement    -> "break" ;
continueStatement -> "continue" ;
returnStatement   -> "return" expression ;
deferStatement    -> "defer" funcCall ;
assertStatement   -> "assert" expression ;
importStatement   -> "import" STRING ;
block             -> "{" declaration* "}" ;
expression        -> assignment ( "?" assignment ":" assignment )? ;
assignment        -> IDENTIFIER "=" assignment
                  | logicalOr ;
logicalOr         -> logicalAnd ( "||" logicalAnd )* ;
logicalAnd        -> equality ( "&&" equality )* ;
equality          -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison        -> term ( ( "<" | "<=" | ">" | ">=" ) term )* ;
term              -> factor ( ( "+" | "-" ) factor )* ;
factor            -> unary ( ( "/" | "*" | "%" ) unary )* ;
unary             -> ( "!" | "-" ) unary
                  | call ;
call              -> funcCall
                  | indexCall ;
funcCall          -> atom ( "(" arguments? ")" )* ;
indexCall         -> atom ( "[" expression "]" )* ;
arguments         -> expression ( "," expression )* ;
atom              -> NUMBER | STRING | "true" | "false" | "nil"
                  | "(" expression ")"
                  | list
                  | map
                  | funDecl
                  | IDENTIFIER ;
list              -> "[" arguments? "]" ;
map               -> "{" mapItems? "}" ;
mapItems          -> expression ":" expression ( "," expression ":" expression )* ;
