program        -> declaration* EOF ;
declaration    -> letDecl
               |  statement ;
letDecl        -> "let" IDENTIFIER ( "=" expression )? ";" ;
statement      -> exprStatement
               | ifStatement
               | printStatement
               | block ;
exprStatement  -> expression ";" ;
ifStatement    -> "if" "(" expression ")" statement ( "else" statement )? ;
printStatement -> "print" expression ";" ;
block          -> "{" declaration* "}" ;
expression     -> assignment ( "?" assignment ":" assignment )? ;
assignment     -> IDENTIFIER "=" assignment
               | logical_or ;
logical_or     -> logical_and ( "||" logical_and )* ;
logical_and    -> equality ( "&&" equality )* ;
equality       -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison     -> term ( ( "<" | "<=" | ">" | ">=" ) term )* ;
term           -> factor ( ( "+" | "-" ) factor )* ;
factor         -> unary ( ( "/" | "*" ) unary )* ;
unary          -> ( "!" | "-" ) unary 
               | atom ;
atom           -> NUMBER | STRING | "true" | "false" | "nil"
               | "(" expression ")"
               | IDENTIFIER ;
